# Basic colors
autoload -Uz colors && colors

# Zsh completion config
autoload -Uz compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' group-name ''
zmodload zsh/complist
compinit
_comp_options+=(globdots) # Show dotfiles

# Vcs for git prompt info
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn hg
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' formats "[%{$fg[blue]%}%b%{$reset_color%}] (%{$fg[yellow]%}%c%{$fg[green]%}%u%{$reset_color%}) %{$fg[yellow]%}%s%{$fg[blue]%}:%r%{$reset_color%}"
precmd() { vcs_info ;} # Run before each prompt

# Vim commandline mode config
bindkey -v
export KEYTIMEOUT=1
vim_ins_mode="%{$fg[yellow]%}[INS]%{$reset_color%}"
vim_cmd_mode="%{$fg[cyan]%}[CMD]%{$reset_color%}"
# vim_vis_mode="%{$fg[red]%}[VIS]%{$reset_color%}"
vim_mode=$vim_ins_mode
function zle-line-init zle-keymap-select {
    case ${KEYMAP} in
        (vicmd) vim_mode=$vim_cmd_mode && echo -ne '\e[1 q' ;;
        (main|viins) vim_mode=$vim_ins_mode && echo -ne '\e[5 q' ;;
        # (visual|viopp) vim_mode=$vim_vis_mode ;;
    esac
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select
echo -ne '\e[5 q'
preexec() { echo -ne '\e[5 q' ;} # Use beam shape cursor for each new prompt.

# Vim keys in tab complete menu:
bindkey -M menuselect "h" vi-backward-char
bindkey -M menuselect "k" vi-up-line-or-history
bindkey -M menuselect "l" vi-forward-char
bindkey -M menuselect "j" vi-down-line-or-history
bindkey -v "^?" backward-delete-char

# Vim bindings in quotations
autoload -U select-quoted
zle -N select-quoted
for m in visual viopp; do
    for c in {a,i}{\',\",\`}; do
        bindkey -M $m $c select-quoted
    done
done

# Vim bindings in brackets
autoload -U select-bracketed
zle -N select-bracketed
for m in visual viopp; do
    for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
        bindkey -M $m $c select-bracketed
    done
done

# Prompt styling
setopt PROMPT_SUBST
PROMPT="%{$fg[red]%}[%{$fg[yellow]%}%n%{$fg[green]%}@%{$fg[blue]%}%M:%{$fg[magenta]%}%c%{$fg[red]%}]%{$reset_color%}$ "
RPROMPT='${vim_mode} ${vcs_info_msg_0_}'

# Expand aliases to actual commands
function expand-alias() {
	zle _expand_alias
	zle self-insert
}
zle -N expand-alias
bindkey -M main ' ' expand-alias

# History file
HISTSIZE=10000
SAVEHIST=10000
HISTFILE=~/.config/zsh/history

# Bind lfcd to ctrl-o
function lfcd {
    tmp="$(mktemp)"
    lf -last-dir-path="$tmp" "$@"
    if [ -f "$tmp" ]; then
        dir="$(cat "$tmp")"
        rm -f "$tmp"
        [ -d "$dir" ] && [ "$dir" != "$(pwd)" ] && cd "$dir"
    fi
}
bindkey -s "^o" "lfcd\n"

# Pip completion
function _pip_completion {
  local words cword
  read -Ac words
  read -cn cword
  reply=( $( COMP_WORDS="$words[*]" \
             COMP_CWORD=$(( cword-1 )) \
             PIP_AUTO_COMPLETE=1 $words[1] ) )
}
compctl -K _pip_completion pip3

# Source files
[ -f "$HOME/.config/zsh/zprofile" ] && source "$HOME/.config/zsh/zprofile"
[ -f "$HOME/.config/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh" ] && source "$HOME/.config/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh"
[ -f "$HOME/.config/zsh/zsh-autosuggestions/zsh-autosuggestions.plugin.zsh" ] && source "$HOME/.config/zsh/zsh-autosuggestions/zsh-autosuggestions.plugin.zsh"
[ -f "$HOME/.config/aliasrc" ] && source "$HOME/.config/aliasrc"
[ -f "$HOME/.config/shortcutrc" ] && source "$HOME/.config/shortcutrc"

# Alias overwrite
alias repr="source $HOME/.config/zsh/zshrc"
